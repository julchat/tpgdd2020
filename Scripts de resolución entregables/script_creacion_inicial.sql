USE GD2C2020
GO

CREATE SCHEMA GESTION_DE_DUENDES
GO

CREATE PROCEDURE GESTION_DE_DUENDES.MigrarTablas
AS BEGIN
	BEGIN TRY
		BEGIN TRANSACTION migrarDatos  --Creo una transaccion para asegurar atomicidad

		--Creacion de tablas finales

		CREATE TABLE GESTION_DE_DUENDES.Transmision(
		Codigo_Transmision decimal(18,0),
		Descripcion nvarchar(255) NOT NULL,

		PRIMARY KEY(Codigo_Transmision)
		);

		CREATE TABLE GESTION_DE_DUENDES.TipoDeAuto(
		Codigo_Tipo_Auto decimal(18,0),
		Descripcion nvarchar(255) NOT NULL,

		PRIMARY KEY(Codigo_Tipo_Auto)
		);

		CREATE TABLE GESTION_DE_DUENDES.Caja(
		Codigo_Caja decimal(18,0),
		Descripcion nvarchar(255) NOT NULL,

		PRIMARY KEY(Codigo_Caja)
		);

		CREATE TABLE GESTION_DE_DUENDES.Modelo(
		Codigo_Modelo decimal(18,0),
		Nombre_Modelo nvarchar(255),
		Codigo_Transmision decimal(18,0) NOT NULL,
		Tipo_Motor decimal(18,0) NOT NULL,
		Codigo_Tipo_Auto decimal(18,0) NOT NULL,
		Potencia decimal(18,0) NOT NULL,
		Codigo_Caja decimal(18,0) NOT NULL,
		Fabricante nvarchar(255) NOT NULL,


		PRIMARY KEY(Codigo_Modelo),
		FOREIGN KEY(Codigo_Transmision) REFERENCES GESTION_DE_DUENDES.Transmision,
		FOREIGN KEY(Codigo_Tipo_Auto) REFERENCES GESTION_DE_DUENDES.TipoDeAuto,
		FOREIGN KEY(Codigo_Caja) REFERENCES GESTION_DE_DUENDES.Caja
		);


		CREATE TABLE GESTION_DE_DUENDES.Autoparte(
		Codigo_Parte decimal(18,0),
		Codigo_Modelo decimal(18,0) NOT NULL,
		Descripcion nvarchar(255) NOT NULL,

		PRIMARY KEY(Codigo_Parte),
		FOREIGN KEY (Codigo_Modelo) REFERENCES GESTION_DE_DUENDES.Modelo
		);

		CREATE TABLE GESTION_DE_DUENDES.Sucursal(
		Direccion nvarchar(255) NOT NULL,
		Ciudad nvarchar(255) NOT NULL,
		Mail nvarchar(255) NOT NULL,
		Telefono decimal(18,0) NOT NULL,

		PRIMARY KEY (Direccion)
		);


		CREATE TABLE GESTION_DE_DUENDES.Cliente(
		ID_Cliente varchar(255),
		Nombre nvarchar(255) NOT NULL,
		Direccion nvarchar(255) NOT NULL,
		Fecha_De_Nac DATETIME2(3) DEFAULT GETDATE(),
		Mail nvarchar(255),
		PRIMARY KEY (ID_Cliente)
		);

		CREATE TABLE GESTION_DE_DUENDES.Compra(
		Numero_De_Compra decimal(18,0),
		Precio decimal(18,2),
		Fecha DATETIME2(3) DEFAULT GETDATE(),
		ID_Sucursal nvarchar(255) NOT NULL,
		ID_Cliente varchar(255) NOT NULL,
		Tipo_De_Compra nvarchar(50) NOT NULL,

		FOREIGN KEY (ID_Sucursal) REFERENCES GESTION_DE_DUENDES.Sucursal,
		FOREIGN KEY (ID_Cliente) REFERENCES GESTION_DE_DUENDES.Cliente,
		PRIMARY KEY (Numero_De_Compra)
		);


		CREATE TABLE GESTION_DE_DUENDES.Automovil(
		Patente nvarchar(50),
		Numero_De_Compra decimal(18,0) NOT NULL,
		Numero_De_Chasis nvarchar(50) NOT NULL,
		Auto_Fecha_Alta DATETIME2(3) DEFAULT GETDATE(),
		Codigo_Modelo decimal(18,0) NOT NULL,
		Cantidad_kms decimal(18,0) NOT NULL,
		Codigo_Motor nvarchar(50) NOT NULL,

		FOREIGN KEY (Codigo_Modelo) REFERENCES GESTION_DE_DUENDES.Modelo,
		FOREIGN KEY (Numero_De_Compra) REFERENCES GESTION_DE_DUENDES.Compra,
		PRIMARY KEY(Patente)
		);

		CREATE TABLE GESTION_DE_DUENDES.Factura(
		Numero_De_Factura decimal(18,0),
		ID_Sucursal nvarchar(255)  NOT NULL,
		Fecha DATETIME2(3) DEFAULT GETDATE(),
		ID_Cliente varchar(255) NOT NULL,
		Precio decimal(18,2),
		Tipo_De_Factura nvarchar(50) NOT NULL,

		FOREIGN KEY (ID_Cliente) REFERENCES GESTION_DE_DUENDES.Cliente,
		FOREIGN KEY (ID_Sucursal) REFERENCES GESTION_DE_DUENDES.Sucursal,
		PRIMARY KEY (Numero_De_Factura)
		);

		CREATE TABLE GESTION_DE_DUENDES.FacturaPorAutoparte(
		Numero_De_Factura decimal(18,0),
		Codigo_Parte decimal(18,0),
		Cantidad decimal(18,0) NOT NULL,
		Precio_Facturado decimal(18,2) NOT NULL

		PRIMARY KEY (Numero_De_Factura, Codigo_Parte),
		FOREIGN KEY (Numero_De_Factura) REFERENCES GESTION_DE_DUENDES.Factura,
		FOREIGN KEY (Codigo_Parte) REFERENCES GESTION_DE_DUENDES.Autoparte
		);

		CREATE TABLE GESTION_DE_DUENDES.FacturaPorAutomovil(
		Numero_De_Factura decimal(18,0),
		Patente nvarchar(50) NOT NULL,

		PRIMARY KEY (Numero_De_Factura),
		FOREIGN KEY (Numero_De_Factura) REFERENCES GESTION_DE_DUENDES.Factura,
		FOREIGN KEY (Patente) REFERENCES GESTION_DE_DUENDES.Automovil
		);


		CREATE TABLE GESTION_DE_DUENDES.CompraPorAutoparte(
		Numero_De_Compra decimal(18,0),
		Codigo_Parte decimal(18,0),
		Cantidad decimal(18,0),
		Precio_Compra decimal (18,2) NOT NULL,

		PRIMARY KEY (Codigo_Parte, Numero_De_Compra),
		FOREIGN KEY (Numero_De_Compra) REFERENCES GESTION_DE_DUENDES.Compra,
		FOREIGN KEY (Codigo_Parte) REFERENCES GESTION_DE_DUENDES.Autoparte
		);

		--Creamos y cargamos tablas temporales

		SELECT DISTINCT COMPRA_NRO,COMPRA_FECHA,COMPRA_PRECIO,AUTO_CANT_KMS,AUTO_FECHA_ALTA,AUTO_NRO_MOTOR,AUTO_NRO_CHASIS,AUTO_PATENTE, CLIENTE_DNI, CLIENTE_APELLIDO, MODELO_CODIGO, SUCURSAL_DIRECCION
		INTO #CompraAuto FROM gd_esquema.Maestra WHERE COMPRA_NRO IS NOT NULL AND AUTO_PATENTE IS NOT NULL

		SELECT DISTINCT FACTURA_NRO, FACTURA_FECHA, PRECIO_FACTURADO, FAC_CLIENTE_DNI, FAC_CLIENTE_APELLIDO, FAC_SUCURSAL_DIRECCION, AUTO_PATENTE
		INTO #VentaAuto FROM gd_esquema.Maestra WHERE FACTURA_NRO IS NOT NULL AND AUTO_PATENTE IS NOT NULL

		SELECT DISTINCT COMPRA_NRO,COMPRA_FECHA,COMPRA_PRECIO, COMPRA_CANT, AUTO_PARTE_CODIGO,AUTO_PARTE_DESCRIPCION, CANT_FACTURADA,FACTURA_NRO, FACTURA_FECHA, PRECIO_FACTURADO, MODELO_CODIGO, CLIENTE_DNI, CLIENTE_APELLIDO, FAC_CLIENTE_APELLIDO, FAC_CLIENTE_DNI, FAC_SUCURSAL_DIRECCION, SUCURSAL_DIRECCION
		INTO #OperacionAutoParte FROM gd_esquema.Maestra WHERE AUTO_PARTE_CODIGO IS NOT NULL

		SELECT DISTINCT CLIENTE_APELLIDO,CLIENTE_NOMBRE,CLIENTE_DIRECCION,CLIENTE_DNI,CLIENTE_FECHA_NAC,CLIENTE_MAIL,
		FAC_CLIENTE_DIRECCION,FAC_CLIENTE_DNI,FAC_CLIENTE_FECHA_NAC,FAC_CLIENTE_MAIL,FAC_CLIENTE_NOMBRE, FAC_CLIENTE_APELLIDO
		INTO #Clientes FROM gd_esquema.Maestra

		SELECT DISTINCT SUCURSAL_CIUDAD,SUCURSAL_DIRECCION,SUCURSAL_MAIL,SUCURSAL_TELEFONO,FAC_SUCURSAL_CIUDAD,FAC_SUCURSAL_DIRECCION,
		FAC_SUCURSAL_MAIL,FAC_SUCURSAL_TELEFONO 
		INTO #Sucursales FROM gd_esquema.Maestra

		SELECT DISTINCT MODELO_CODIGO, MODELO_NOMBRE, MODELO_POTENCIA, TIPO_AUTO_CODIGO, TIPO_AUTO_DESC,
		TIPO_CAJA_CODIGO, TIPO_CAJA_DESC, TIPO_MOTOR_CODIGO, TIPO_TRANSMISION_CODIGO, TIPO_TRANSMISION_DESC,
		FABRICANTE_NOMBRE INTO #Modelos FROM gd_esquema.Maestra WHERE AUTO_PATENTE IS NOT NULL 

		--Comenzamos a llenar las tablas finales

		INSERT INTO GESTION_DE_DUENDES.Transmision (Codigo_Transmision, Descripcion) 
		SELECT DISTINCT TIPO_TRANSMISION_CODIGO, TIPO_TRANSMISION_DESC FROM #Modelos

		INSERT INTO GESTION_DE_DUENDES.TipoDeAuto (Codigo_Tipo_Auto, Descripcion)
		SELECT DISTINCT TIPO_AUTO_CODIGO, TIPO_AUTO_DESC FROM #Modelos
		
		INSERT INTO GESTION_DE_DUENDES.Caja (Codigo_Caja, Descripcion)
		SELECT DISTINCT TIPO_CAJA_CODIGO, TIPO_CAJA_DESC FROM #Modelos
		
		INSERT INTO GESTION_DE_DUENDES.Modelo(Codigo_Modelo, Nombre_Modelo, Codigo_Transmision, Tipo_Motor, Codigo_Tipo_Auto, Potencia, Codigo_Caja, Fabricante)
		SELECT DISTINCT MODELO_CODIGO, MODELO_NOMBRE, TIPO_TRANSMISION_CODIGO, TIPO_MOTOR_CODIGO, TIPO_AUTO_CODIGO, MODELO_POTENCIA, TIPO_CAJA_CODIGO, 
		 FABRICANTE_NOMBRE FROM #Modelos
		
		INSERT INTO GESTION_DE_DUENDES.Autoparte(Codigo_Parte, Codigo_Modelo, Descripcion)
		SELECT DISTINCT AUTO_PARTE_CODIGO, MODELO_CODIGO, AUTO_PARTE_DESCRIPCION FROM #OperacionAutoParte

		INSERT INTO GESTION_DE_DUENDES.Sucursal(Direccion,Ciudad,Mail,Telefono)
		(SELECT DISTINCT SUCURSAL_DIRECCION, SUCURSAL_CIUDAD, SUCURSAL_MAIL, SUCURSAL_TELEFONO FROM #Sucursales WHERE SUCURSAL_DIRECCION IS NOT NULL
		UNION
		SELECT DISTINCT FAC_SUCURSAL_DIRECCION, FAC_SUCURSAL_CIUDAD, FAC_SUCURSAL_MAIL, FAC_SUCURSAL_TELEFONO FROM #Sucursales WHERE FAC_SUCURSAL_DIRECCION IS NOT NULL
		)

		INSERT INTO GESTION_DE_DUENDES.Cliente(ID_Cliente,Nombre, Fecha_De_Nac, Mail, Direccion)
		(SELECT DISTINCT CAST(CLIENTE_DNI AS varchar(255)) + '-' + CLIENTE_APELLIDO , CLIENTE_NOMBRE, CLIENTE_FECHA_NAC, CLIENTE_MAIL, CLIENTE_DIRECCION FROM #Clientes WHERE CLIENTE_DNI IS NOT NULL
		UNION
		SELECT DISTINCT CAST(FAC_CLIENTE_DNI AS varchar(255)) + '-' + FAC_CLIENTE_APELLIDO, FAC_CLIENTE_NOMBRE, FAC_CLIENTE_FECHA_NAC, FAC_CLIENTE_MAIL, FAC_CLIENTE_DIRECCION FROM #Clientes WHERE FAC_CLIENTE_DNI IS NOT NULL
		)

		INSERT INTO GESTION_DE_DUENDES.Compra(Numero_De_Compra, Precio, Fecha, ID_Cliente, ID_Sucursal, Tipo_De_Compra)
		SELECT DISTINCT COMPRA_NRO, COMPRA_PRECIO, COMPRA_FECHA, CAST(CLIENTE_DNI AS varchar(255)) + '-' + CLIENTE_APELLIDO,SUCURSAL_DIRECCION, 'Automovil'  FROM #CompraAuto

		INSERT INTO GESTION_DE_DUENDES.Compra(Numero_De_Compra, Fecha, ID_Cliente, ID_Sucursal, Tipo_De_Compra, Precio)
		SELECT COMPRA_NRO, COMPRA_FECHA, CAST(CLIENTE_DNI AS varchar(255)) + '-' + CLIENTE_APELLIDO, SUCURSAL_DIRECCION, 'Autoparte', SUM(COMPRA_CANT * COMPRA_PRECIO)  FROM #OperacionAutoParte
		WHERE COMPRA_NRO IS NOT NULL
		GROUP BY COMPRA_NRO, COMPRA_FECHA, CAST(CLIENTE_DNI AS varchar(255)) + '-' + CLIENTE_APELLIDO, SUCURSAL_DIRECCION

		INSERT INTO GESTION_DE_DUENDES.CompraPorAutoparte(Numero_De_Compra, Codigo_Parte,Precio_Compra, Cantidad)
		SELECT COMPRA_NRO, AUTO_PARTE_CODIGO, COMPRA_PRECIO, SUM(COMPRA_CANT) FROM #OperacionAutoParte
		WHERE COMPRA_NRO IS NOT NULL
		GROUP BY COMPRA_NRO, AUTO_PARTE_CODIGO, COMPRA_PRECIO

		INSERT INTO GESTION_DE_DUENDES.Automovil (Patente, Codigo_Modelo, Codigo_Motor, Numero_De_Chasis, Numero_De_Compra, Auto_Fecha_Alta, Cantidad_kms)
		SELECT DISTINCT AUTO_PATENTE, MODELO_CODIGO, AUTO_NRO_MOTOR, AUTO_NRO_CHASIS, COMPRA_NRO, AUTO_FECHA_ALTA, AUTO_CANT_KMS FROM #CompraAuto

		INSERT INTO GESTION_DE_DUENDES.Factura(Numero_De_Factura, Precio, Fecha, ID_Cliente, ID_Sucursal, Tipo_De_Factura)
		SELECT DISTINCT FACTURA_NRO, PRECIO_FACTURADO, FACTURA_FECHA, CAST(FAC_CLIENTE_DNI AS varchar(255)) + '-' + FAC_CLIENTE_APELLIDO, FAC_SUCURSAL_DIRECCION, 'Automovil'  FROM #VentaAuto
		WHERE FACTURA_NRO IS NOT NULL

		INSERT INTO GESTION_DE_DUENDES.FacturaPorAutomovil(Numero_De_Factura,Patente)
		SELECT DISTINCT FACTURA_NRO, AUTO_PATENTE FROM #VentaAuto

		INSERT INTO GESTION_DE_DUENDES.Factura(Numero_De_Factura, Fecha, ID_Cliente, ID_Sucursal, Tipo_De_Factura, Precio)
		SELECT FACTURA_NRO, FACTURA_FECHA, CAST(FAC_CLIENTE_DNI AS varchar(255)) + '-' + FAC_CLIENTE_APELLIDO, FAC_SUCURSAL_DIRECCION, 'Autoparte', SUM(CANT_FACTURADA * PRECIO_FACTURADO)  FROM #OperacionAutoParte
		WHERE FACTURA_NRO IS NOT NULL
		GROUP BY FACTURA_NRO, FACTURA_FECHA, CAST(FAC_CLIENTE_DNI AS varchar(255)) + '-' + FAC_CLIENTE_APELLIDO, FAC_SUCURSAL_DIRECCION

		INSERT INTO GESTION_DE_DUENDES.FacturaPorAutoparte(Numero_De_Factura, Codigo_Parte, Precio_Facturado, Cantidad)
		SELECT FACTURA_NRO, AUTO_PARTE_CODIGO, PRECIO_FACTURADO, SUM(CANT_FACTURADA) FROM #OperacionAutoParte
		WHERE FACTURA_NRO IS NOT NULL
		GROUP BY FACTURA_NRO, AUTO_PARTE_CODIGO, PRECIO_FACTURADO
		COMMIT TRANSACTION migrarDatos --Si se llego hasta acá, commiteamos
			END TRY
			BEGIN CATCH --Si hubo algún fallo, entra por aca y hacemos rollback
			IF(@@TRANCOUNT>0)
			BEGIN
				raiserror('Hubo un error al migrar los datos de la concesionaria',16,1)
				SELECT
				ERROR_NUMBER() AS ErrorNumber,
				ERROR_STATE() AS ErrorState,
				ERROR_SEVERITY() AS ErrorSeverity,
				ERROR_PROCEDURE() AS ErrorProcedure,
				ERROR_LINE() AS ErrorLine,
				ERROR_MESSAGE() AS ErrorMessage;
			END
			ROLLBACK TRANSACTION migrarDatos
		END CATCH
END
GO

EXEC GESTION_DE_DUENDES.MigrarTablas
